* cel48A(sub)+bglc
* mutant: pore -> phe

bomblev 0

open unit 12 read form name /home/rumack/charmm/cel48a/top_all36_carb.rtf
read rtf card unit 12
close unit 12

open unit 13 read form name /home/rumack/charmm/cel48a/par_all36_carb_update.prm
read param card flex unit 13
close unit 13

open unit 10 read form name /home/rumack/charmm/cel48a/top_all22_prot_remas.rtf
read rtf card append unit 10
close unit 10
open unit 11 read form name /home/rumack/charmm/cel48a/par_all22_prot_remas.prm
read param card flex append unit 11
close unit 11


read sequ pdb name cel48a_fit_rc.pdb
GENERATE PROT setup

read coor pdb name cel48a_fit_rc.pdb
ic build
hbuild

bomb -1

! Delete side chain atoms (delete everything not in backbone)
delete atom select (resid 97 .and. .not. (type n .or. type ca .or. type c .or. type o .or. type ha .or. type hn)) end

rename resname ala select resid 97 end

! Delete side chain atoms (delete everything not in backbone)
delete atom select (resid 213 .and. .not. (type n .or. type ca .or. type c .or. type o .or. type ha .or. type hn)) end

rename resname ala select resid 213 end

! Store the mutated protein that lacks the mutated side chain
open write card name temp.pdb unit 14
write coor pdb select all end unit 14
* Temporary
*
close unit 14

! Remove all memory of the protein
delete atom select all end

! Read protein back in, with mutated sequence
open read card name temp.pdb unit 14
read sequence pdb unit 14
generate prot setup
rewind unit 14
read coor pdb unit 14
close unit 14

ic fill preserve
ic parameter
ic build
hbuild


! CEL48A pore residues
define pore sele ( segid PROT .and. ( -
resid 50 .or. resid 51 .or. resid 55 .or. -
resid 58 .or. resid 222 .or. resid 224 .or. -
resid 225 .or. resid 228 .or. resid 231 .or. -
resid 235 .or. resid 238 .or. resid 239 .or. -
resid 326 .or. resid 328 .or. resid 420 .or. -
resid 423 .or. resid 424 .or. -
resid 621 .or. resid 62 .or. resid 65 .or. -
resid 427 .or. resid 430 .or. resid 504 .or. -
resid 507 .or. resid 511 .or. resid 625 .or. -
resid 628 .or. resid 632 .or. resid 636 .or. -
resid 431 .or. resid 635 ) ) end

cons fix sele all .and. .not. pore end

shake bonh para
mini sd nstep 30

SHAKe BONH PARAm
SCAL FBETA SET 60.0 SELE pore END
DYNA LANG LEAP STRT -
   TIMEstep 0.001 -     !timestep in picoseconds
   NSTEp 1000 -         !number of steps and energy evaluations
   NPRInt 100 -         !step frequency for writing in kunit and printing energy on unit 6
   IUNWri 41 -          !unit to write restart file
   IUNCrd 31 -          !unit to write coordinated (unformatted)
   IUNRea -1 -          !unit to read in restart file
   KUNIt -1 -           !unit to write temperature and total energy
   NSAVc 10 -           !frequency for writing coordinates
   NSAVv 10 -           !frequency for writing velocities
   IHBFrq 0 -           !frequency to regenerate hydrogen bond list
   INBFrq 25 -          !lists updated when necessary (heuristic test)
   ISEEd 314159 -       !seed for the random number generator in assigning velocities
   NBXMod 5 -           !Add 1-2 1-3 and special 1-4 interactions
   ATOM -               !electrostatics calculation (default) is done on atom-atom basis
   CDIE -               !energy ~ 1/r
   FSWItch -            !use switching
   VATOm -              !needed for nonbond list generation
   VSWItch -            !use switching function for van der Waals
   CUTNb 16.0 -         !distance cutoff for neighbor list
   CTOFnb 12.0 -        !max distance for pair to consider in energy
   CTONnb 8. -          !Distance at which smoothing function reduces pair's contribution
   INBFrq -1 -          !lists updated when necessary (heuristic test)
   EPS 1. -             !dielectric potential
   E14FAC 1.0 -         !electrostatic scale factor
   WMIN 1.0 -           !warning cutoff for minimum atom-atom distance
   TBATh 300 -          !temperature of heath bath
   FIRStt 300 -         !initial temperature of the system
   FINAlt 300 -         !final temperature of the system
   TEMInc 10.0 -        !value to increase temperature by
   IASOrs 1 -           !assign (NOT scale) velocities during heating/equil
   IASVel 1 -           !use gaussian distribution of velocities
   ISCVel 1 -           !single scale factor
   ICHEcw 0 -           !checks if  avg temp. lies within a window
   TWINdh 0.0           !Highest deviation allowed of FINALT on the high side

write coor pdb name cel48a_y97a_w313a_step1.pdb

write coor card name cel48a_y97a_w313a_step1.cor

write psf card name cel48a_y97a_w313a_step1.psf
* psf
*

stop

